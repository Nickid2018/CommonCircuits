plugins {
    id("fabric-loom").version("1.1-SNAPSHOT").apply(false)
    id("org.ajoberstar.grgit").version("5.0.0")
    // https://github.com/ReplayMod/preprocessor
    id("com.replaymod.preprocess").version("SNAPSHOT")
}

// Preprocessor graph
preprocess {
    def mc1165 = createNode("1.16.5", 1_16_05, "mojang")
    def mc1171 = createNode("1.17.1", 1_17_01, "mojang")
    def mc1182 = createNode("1.18.2", 1_18_02, "mojang")
    def mc1193 = createNode("1.19.3", 1_19_03, "mojang")

    mc1193.link(mc1182, file("versions/mapping-1.19.3-1.18.2.txt"))
    mc1182.link(mc1171, null)
    mc1171.link(mc1165, null)
}

String getBuildVersion() {
    def name = project.mod_version
    if (grgit == null)
        name += "-gitless"
    else {
        name += "-patch.${grgit.log().size()}-"
        def lastCommit = grgit.log(maxCommits: 1)
        name += lastCommit == null ? "uncommited" : lastCommit.first().abbreviatedId
    }
    def env = System.getenv()
    if (env.GITHUB_RUN_NUMBER != null)
        name += "-${env.GITHUB_RUN_NUMBER}"
    else
        name += "-local"
    if (env.BUILD_TYPE != null)
        name += "-${env.BUILD_TYPE}"
    else
        name += "-dev"
    return name
}

setVersion(getBuildVersion())